# Copyright (c) 2000, 2001 Upi Tamminen <desaster@dragonlight.fi>
# See the COPYRIGHT file for more information

package table

alias h.table (h.columns, h.level, h.data) {
    @ :h.c = 0
    @ :h.n = numwords($h.data)
    @ :h.lparam = (h.level != [NONE] ? [-level $h.level] : [])
    fe ($h.data) h.text {
	if (count(" " $h.text)) {
	    push :${[h.table.c] ## h.c} "$h.text"
	} else {
	    push :${[h.table.c] ## h.c} $h.text
	}
	@ :h.c++
	if (h.c == h.columns) { @ :h.c = 0 }
    }

    @ :h.row = [.]
    for (@ :h.c = 0, h.c < h.columns, @ h.c++) {
	@ :h.row #= [-$repeat($maxlen($(${[h.table.c] ## h.c})) -)-]
	@ :h.row #= (h.c == h.columns - 1) ? [.] : [+]
    }
    xecho -b $h.lparam $h.row

    @ :h.row = [|]
    for (@ :h.c = 0, h.c < h.columns, @ :h.c++) {
	@ :h.w = shift(h.data)
	@ :h.row #= [ $pad($maxlen($(${[h.table.c] ## h.c})) " " $h.w) |]
    }
    xecho -b $h.lparam $h.row

    @ :h.row = [+]
    for (@ :h.c = 0, h.c < h.columns, @ h.c++) {
	@ :h.row #= [-$repeat($maxlen($(${[h.table.c] ## h.c})) -)-+]
    }
    xecho -b $h.lparam $h.row

    @ :h.c = 0
    @ :h.row = [|]
    @ :h.w = shift(h.data)
    while (h.w) {
	@ :h.row #= [ $pad($maxlen($(${[h.table.c] ## h.c})) " " $h.w) |]
	@ :h.c++
	if (h.c == h.columns) {
	    xecho -b $h.lparam $h.row
	    @ :h.row = [|]
	    @ :h.c = 0
	}
	@ :h.w = shift(h.data)
    }

    @ :h.row = [`]
    for (@ :h.c = 0, h.c < h.columns, @ h.c++) {
	@ :h.row #= [-$repeat($maxlen($(${[h.table.c] ## h.c})) -)-]
	@ :h.row #= (h.c == h.columns - 1) ? ['] : [+]
    }
    xecho -b $h.lparam $h.row
}

# vim: set ai tw=75 sw=4:
