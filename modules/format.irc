# Copyright (c) 1999-2014 Upi Tamminen <desaster at gmail>
# See the COPYRIGHT file for more information

package format

eval on #-hook $hash_32bit(format) "post_load" {
    h.lagmeter.refresh
}

alias h.format.set (h.name, h.data) {
    if (h.name && h.data) {
        assign h[formats][$h.name] $h.data
    }
    if (left(1 $h.name) == [-] && !h.data) {
        assign -h[formats][$rest(1 $h.name)]
    }
}

alias h.format.parse (h.name, ...) {
    @ :h.data = h[formats][$h.name]
    @ sar(gr/%/%%/h[data])
    @ sar(gr/&/%/h[data])
    @ :h.data = cparse($restw(0 $h[data]))
    if (h[formats][$h.name]) {
        if (functioncall()) {
            eval return $h[data]
        } else {
            eval echo $h[data]
        }
    }
}

alias h.format.value (h.name) {
    @ :h.data = h[formats][$h.name]
    @ sar(gr/%/%%/h.data)
    @ sar(gr/&/%/h.data)
    eval return $h.data
}

alias rstamp {
    if (h.cfg.timestamp_strftime) {
        @ :h.ts = [$strftime($h.cfg.timestamp_strftime)]
    } else {
        @ :h.ts = [$Z]
    }
    return $h.ts
}

alias stamp h.format.stamp
alias h.format.stamp {
    if (h.cfg.timestamp_strftime) {
        @ :h.ts = [$strftime($h.cfg.timestamp_strftime)]
    } else {
        @ :h.ts = [$Z]
    }
    if (h.cfg.timestamp) { return $cparse($h.format.parse(timestamp $h.ts)) }

}

alias nickstatus (h.nick, h.channel, ...) {
    if (ischanop($h.nick $h.channel)) {
        return @
    } elsif (ischanvoice($h.nick $h.channel)) {
        return +
    }
}

alias h.lagmeter.refresh {
    ^timer -delete lagmeter
    if (!strlen($myservers())) { return }
    fe ($myservers()) h.s {
        @ h.lagmeter[$h.encode($h.s)][lag] = [?]
        ^xquote -server $servernum($h.s) ping :$h.s
        @ h.lagmeter[$h.encode($h.s)][time] = time()
    }
    h.lagmeter.status_update $winnum()

    ^on ^pong "*" {
        @ :h.eserver = h.encode($0)
        if (strlen($h.lagmeter[$h.eserver][time])) {
            @ h.lagmeter[$h.eserver][lag] = (time() - \
                h.lagmeter[$h.eserver][time])
            h.lagmeter.status_update $winnum()
        }
    }
    timer -refnum lagmeter 60 { h.lagmeter.refresh }
}

alias h.lagmeter.status_update (window, ...) {
    @ :h.s = servername($winserv($window))
    @ :h.lag = h[lagmeter][$h.encode($h.s)][lag]
    if (!strlen($h.lag)) { @ :h.lag = [?] }
    ^set status_user1 $sar(/%1/$h.lag/$cparse($h.format.value(status_lag)))
}

alias h.set.idle {
    if (S) {
        @ :h.it = tdiff2($h.idletime)
        if (match(*s $h.it)) { @ pop(h.it) }
        if (!numwords($h.it)) { @ :h.it = [0m] }
        ^set status_user3 $cparse($h.format.parse(status_idle $h.it))
    } else {
        ^set status_user3 $cparse($h.format.parse(status_idle ?))
    }
}

alias h.reset.idle {
    @ h.idletime = 0
    h.refresh.idle 1
}

alias h.refresh.idle (h.idle_reset) {
    if (S) {
        ^timer -delete h.idle
        if (!h.idle_reset) { @ h.idletime += 60 }
        h.set.idle
        ^timer -refnum h.idle 60 h.refresh.idle
    } else {
        ^timer -delete h.idle
        h.set.idle
    }
}

alias h.refresh.uptime {
    @ :h[u] = tdiff2(${time() - F})
    if (match(*s $h[u])) { @ pop(h[u]) }
    if (!numwords($h[u])) { @ :h[u] = [0m] }
    ^set status_user2 $cparse($h.format.parse(status_uptime $h[u])%N)
}

alias h.refresh.dcc {
    @ :h.s = (h.dcc.sendcount ? h.dcc.sendcount : [0])
    @ :h.g = (h.dcc.getcount ? h.dcc.getcount : [0])
    ^set status_user4 $cparse($h.format.parse(status_dcc $h.s $h.g)%N)
}

alias h.refresh.away {
    if (h.awaycount == []) { @ h.awaycount = 0 }
    ^set status_away $cparse($h.format.parse(status_away $h.awaycount)%N)
}

alias colorifynick16 (h.nick, ...) {
    @ :h.n = right(1 $hash_32bit($strip(@+ $h.nick)))
    @ :h.c = [%r %R %g %G %B %B %m %M %c %C]
    return $cparse($word($h.n $h.c)$h.nick)
}

# ”<Esc>[38;5;ColorNumberm”
@ h.colorifynick_256colors = [1 1 2 3 4 5 6 7 21 9 10 11 12 13 14 15 29 30 18 19 20 21 22 23 24 25 26 27 28 29 30 31 32 33 34 35 36 37 38 39 40 41 42 43 44 45 46 47 48 49 50 51 64 53 54 55 56 57 58 59 60 61 62 63 64 65 66 67 68 69 70 71 72 73 74 75 76 77 78 79 80 81 82 83 84 85 86 87 88 89 90 91 92 93 94 95 96 97 98 99 100 101 102 103 104 105 106 107 108 109 110 111 112 113 114 115 116 117 118 119 120 121 122 123 124 125 126 127 128 129 130 131 132 133 134 135 136 137 138 139 140 141 142 143 144 145 146 147 148 149 150 151 152 153 154 155 156 157 158 159 160 161 162 163 164 165 166 167 168 169 170 171 172 173 174 175 176 177 178 179 180 181 182 183 184 185 186 187 188 189 190 191 192 193 194 195 196 197 198 199 200 201 202 203 204 205 206 207 208 209 210 211 212 213 214 215 216 217 218 219 220 221 222 223 224 225 226 227 228 229 230 231 205 206 207 208 209 210 238 239 240 241 242 243 244 245 246 247 248 249 250 251 252 253 254 255]
alias colorifynick256 (h.nick, ...) {
    @ :h.n = [${hash_32bit($h.nick) & 255}]
    @ :h.n = word($h.n $h.colorifynick_256colors)
    h.debug colorifynick\($h.nick\) = $h.n
    @ :retval = [$chr(27)\[38\;5\;${h.n}m${h.nick}$chr(27)\[0m]
    return $retval
}

alias colorifynick (h.nick, ...) {
    if (h.cfg.use_256_colors) {
        return $colorifynick256($h.nick)
    } else {
        return $colorifynick16($h.nick)
    }
}

# vim: set ai tw=75 sw=4 et:
